@extend(yours.before_init_transfer)
function before_init_transfer(yours.token, ft4.accounts.account) {
  delete placed_position @? { .placeable.token == token };
}

@extend(yours.after_apply_transfer) 
function after_apply_transfer(yours.token, modules: set<name>, attributes: map<text, gtv>) {
  if (not modules.contains("placeables")) return;

  if (placeable @? { token } == null) {
    create placeable(
      token, 
      height = integer.from_gtv(attributes.get("height")), 
      width = integer.from_gtv(attributes.get("width")));
  }
}

@extend(yours.populate_metadata)
function populate_metadata(yours.token, modules: set<name>) {
  if (not modules.contains("placeables")) return null;

  val placeable = placeable @? { token };
  if (placeable == null) return null;

  val metadata = map<text, gtv>();
  metadata.put("height", placeable.height.to_gtv());
  metadata.put("width", placeable.width.to_gtv());

  return metadata;
}